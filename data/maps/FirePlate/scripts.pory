mapscripts FirePlate_MapScripts {
    MAP_SCRIPT_ON_LOAD: FirePlate_OnLoad
}

script FirePlate_OnLoad {
    call(FirePlate_IsGemCollected)
    call(FirePlate_ConditionCheck)
    call_if_unset(FLAG_FIRE_PC, FirePlate_Entrance)
    end
}

script FirePlate_Entrance {
    setmetatile(6, 15, METATILE_Cave_EntranceCover, TRUE)
    setmetatile(7, 15, METATILE_Cave_EntranceCover, TRUE)
    setmetatile(8, 15, METATILE_Cave_EntranceCover, TRUE)
    setmetatile(6, 16, METATILE_Cave_SealedChamberBraille_Mid, TRUE)
    setmetatile(7, 16, METATILE_Cave_SealedChamberBraille_Mid, TRUE)
    setmetatile(8, 16, METATILE_Cave_SealedChamberBraille_Mid, TRUE)
    return
}

script FirePlate_Battle {
    lockall
    playmoncry(SPECIES_ARCANINE_HISUI, CRY_MODE_ENCOUNTER)
    delay(40)
    waitmoncry
    
    createmon(B_SIDE_OPPONENT, 0, SPECIES_ARCANINE_HISUI, 50, [ITEM_FIRE_GEM], , NATURE_JOLLY, 2, , 252, 252, 252, 252, 252, 252, 31, 31, 31, 31, 31, 31, [MOVE_DOUBLE_EDGE], [MOVE_WILD_CHARGE], [MOVE_HEAD_SMASH], [MOVE_FLARE_BLITZ])
    settotemboost(B_BATTLER_1, 2, 0, 2, 0, 0, 1, 0)
    setflag(FLAG_B_SMART_WILD_AI)
    setflag(FLAG_B_NO_CATCHING)
    setflag(FLAG_B_NO_BAG_USE)
    //setvar(VAR_B_VAR_STARTING_STATUS, )
    dowildbattle
    specialvar(VAR_RESULT, GetBattleOutcome)
    if(var(VAR_RESULT) == B_OUTCOME_CAUGHT){
        call(FirePlateWonCatch)
    }
    elif(var(VAR_RESULT) == B_OUTCOME_WON){
        call(FirePlateWonCatch)
    }
    elif(var(VAR_RESULT) == B_OUTCOME_RAN){
        call(FirePlateRun)
    }
    elif(var(VAR_RESULT) == B_OUTCOME_PLAYER_TELEPORTED){
        call(FirePlateRun)
    }
    waitmessage
    clearflag(FLAG_B_NO_BAG_USE)
    clearflag(FLAG_B_NO_CATCHING)
    clearflag(FLAG_B_SMART_WILD_AI)
    setflag(FLAG_FIRE_PC1)
    releaseall
}

script FirePlateWonCatch {
    fadescreen(FADE_TO_BLACK)
    removeobject(1)
    fadescreen(FADE_FROM_BLACK)
    msgbox("The Arcanine dissapeared in to the\ncave.", [MSGBOX_DEFAULT])
    giveitem(ITEM_FLAME_PLATE)
    msgbox("There is an engraving on the Plate.", [MSGBOX_DEFAULT])
    msgbox("The power of defeated giants infuses\nthis Plate.", [MSGBOX_DEFAULT])
    return
}

script FirePlateRun {
    playmoncry(SPECIES_ARCANINE_HISUI, CRY_MODE_ENCOUNTER)
    waitmoncry
    return
}

script FirePlate_IsGemCollected {
    if(flag(FLAG_HIDDEN_ITEM_FIRE_GEM) == TRUE){
        setmetatile(7, 21, METATILE_Cave_ShoalCave_BlueStone_Small, TRUE)
    }
    else{
        setmetatile(7, 21, METATILE_Cave_ShoalCave_BlueStone_Large, TRUE)
    }
    return
}

script FirePlate_CollectGem {
    lockall
    if(flag(FLAG_HIDDEN_ITEM_FIRE_GEM) == TRUE){
        msgbox("The Gem Cluster has Shrunken", MSGBOX_DEFAULT)
    }
    else{
        msgbox("There is a large gem cluster here,\nCollect it?", MSGBOX_YESNO)
        if(var(VAR_RESULT) == 1){
            giveitem(ITEM_FIRE_GEM)
            setflag(FLAG_HIDDEN_ITEM_FIRE_GEM)
            fadescreen(FADE_TO_BLACK)
            warp(MAP_FIRE_PLATE, 7, 20)
            setmetatile(7, 21, METATILE_Cave_ShoalCave_BlueStone_Small, TRUE)
            fadescreen(FADE_FROM_BLACK)
        }
    }
    releaseall
}

script FirePlate_ConditionCheck {
    if(flag(FLAG_CAUGHT_MESPIRT) == TRUE){
        if(flag(FLAG_CAUGHT_AZELF) == TRUE){
            if(flag(FLAG_CAUGHT_UXIE) == TRUE){
                checkitem(ITEM_FIRE_GEM, 1)
                if(var(VAR_RESULT) == TRUE){
                    setflag(FLAG_FIRE_PC)
                }
            }
        }
    }
    return
}